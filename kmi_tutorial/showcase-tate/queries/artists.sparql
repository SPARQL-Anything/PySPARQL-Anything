PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX tate: <http://sparql.xyz/example/tate/>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  ?artist a schema:Person;
  	rdfs:label ?name;
	schema:gender ?gender;
	schema:birthDate ?yearOfBirth;
	schema:deathDate ?yearOfDeath;
	schema:birthPlace ?placeOfBirth;
	schema:deathPlace ?placeOfDeath;
	schema:url ?url .
} WHERE {
  # Artists from the Tate Gallery open data!
  SERVICE <x-sparql-anything:csv.headers=true,location=./collection/artist_data.csv> {
    []  xyz:id ?id;
	    xyz:name ?name;   
  	    xyz:gender ?gender;   
  		xyz:yearOfBirth ?yearOfBirth;   
  		xyz:yearOfDeath ?yearOfDeath;   
  		xyz:placeOfBirth ?placeOfBirth;   
  		xyz:placeOfDeath ?placeOfDeath;   
		xyz:url ?url
  }
  # BIND ( fx:entity (tate:, "artist-", ?id ) as ?artist ) .
  BIND (IRI(CONCAT(STR(tate:), "artist-", ?id )) AS ?artist) .
}



